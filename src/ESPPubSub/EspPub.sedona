//@niagaraIcon="module://icons/x16/io.png"
class EspPub
  extends Component    
{
  @defaultOnClone
  @config @asStr property Buf(64) Topic
  @config @asStr property Buf(64) Payload
  //@config @asStr property Buf(16) QOS
  @config property bool Enable = false
  @config @readonly property int Pub_ID = -1

  property bool asBool = null
  property int asInt
  property float asFloat = null

  define Log log
  virtual override void stop()
  {
    if (Pub_ID != -1) EspPubSub.doEspPublishDeinit(Pub_ID);
    Pub_ID := -1;
	log.message("MQTT Publisher app is stopped!\n")
  }

  virtual override void start()
  {
	// Don't touch hardware until input value is valid
	if ( !Sys.app.isSteadyState() ) 
		return

    if (Pub_ID != -1) EspPubSub.doEspPublishDeinit(Pub_ID);
    Pub_ID := -1;
	Str topicS = Topic.toStr();
	if (topicS.length() > 0) {
	  Pub_ID := EspPubSub.doEspPublishInit(topicS);
      state = 1
    }
  }

  override void changed(Slot slot)
  {
	super.changed(slot)
	// Don't respond to prop changes while app is loading
	if (!Sys.app.isRunning()) return

    // If topic changes, re-init the EspPub
    if (slot == EspPub.Topic) {
      state = 0
	} else if (slot == EspPub.asBool) {
      pubType = 1
    } else if (slot == EspPub.asInt) {
      pubType = 2
    } else if (slot == EspPub.asFloat) {
      pubType = 3
    }
  }

  virtual override void execute()
  {
    // If not yet initialized, do that first
    if (state <= 0)
    {
      start()
      return
    }
    if ((Enable == true) &&
        (pubType > 0)) {
      float val = 0.0;
      if (pubType == 1) {
        val = asBool?1.0:0.0
		asInt := (int)val
		asFloat := val
      } else if (pubType == 2) {
        val = (float)asInt
        if (val == 0.0) {
          asBool := false
        } else {
          asBool := true
        }
        asFloat := val;
      } else if (pubType == 3) {
        val = asFloat;
        if (val == 0.0) {
          asBool := false
        } else {
          asBool := true
        }
        asInt := (int)val
      }
      pubType = 0;
      if (val != value) {
        value = val;
        EspPubSub.doEspPublishMsg(Pub_ID, value)
      }
    }
  }

  float value = null
  int state = 0
  int pubType = 0
}